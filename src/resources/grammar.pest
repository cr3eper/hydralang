WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!("#"|NEWLINE) ~ ANY)* ~ ("#"|NEWLINE) } 

number        = @{ ASCII_DIGIT+ }
operator      =  { "+" | "-" | "*" | "/" | "^" | "$" | "%" }
var           = @{ !("where") ~ ASCII_ALPHA+ }
vector        =  { "(" ~ expr ~ ("," ~ expr)* ~ ")" }
function_call =  { function_name ~ "(" ~ expr ~ ("," ~ expr)* ~ ")" }


token = _{ number | operator | function_call | var | vector }

constraint      =  { "where" ~ "{" ~ (constraint_item ~ ("," ~ constraint_item)*)~ "}" }
constraint_item = _{ conditional | equality }
conditional     = ${ var ~ " "+ ~ ("is" | "not") ~ " "+ ~ function_call }
comparison      =  { "=" | "!=" | "<=" | ">=" | "<" | ">" }
range           =  { (number ~ comparison){0, 1} ~ var ~ (comparison ~ number){0, 1} }
type            =  { "Num" | "Var" | "Function" | "Any" | "Constant" }

function_name = @{ ASCII_ALPHA+ }
function_def  =  { function_head ~ statement ~ constraint{0, 1} ~ NEWLINE* }
function_head = ${ function_name ~ "(" ~ " "* ~ expr ~ (" "* ~ "," ~ " "* ~ expr ~ " "*)* ~ ")" ~ " "* ~ "=" }

expr      = { token+ }
statement = { expr ~ NEWLINE* }
equality  = { expr ~ comparison ~ expr ~ NEWLINE* }
script    = { (function_def | equality | statement)+ }